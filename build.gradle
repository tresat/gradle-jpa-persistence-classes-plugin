apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'eclipse'

repositories {
  jcenter()
}

dependencies {
  def groovy = [group:'org.codehaus.groovy', name:'groovy-all', version:'2.4.7']
  
  compile gradleApi()
  compile groovy

  compile(group: 'org.reflections', name: 'reflections', version: '0.9.10')
  compile(group: 'javax.servlet', name: 'javax.servlet-api', version: '4.0.0-b01')
  compile(group: 'javax.persistence', name: 'persistence-api', version: '1.0.2')
}

description = 'Gradle JPA Persistence.xml classes plugin'
sourceCompatibility = 1.8
group = 'com.tomtresansky.gradle.plugin.jpa.persistence'
version = '0.1'

eclipse {
  project {
    name = 'Gradle-JPA-Persistence-Classes-Plugin'
    natures += 'org.springsource.ide.eclipse.gradle.core.nature'
  }

  classpath {
    // Override default setting and download and associate Javadoc
    downloadJavadoc = true
  }
}

/*
 * Remove 'default classpath' from generated build paths, else the project root becomes a source directory.
 */
eclipse.classpath.file {
  // Closure executed after .classpath content is loaded from existing file and after gradle build information is merged
  whenMerged { classpath ->
  	classpath.entries.removeAll { entry -> 
  	  return (entry.kind == 'src' && entry.path == "/${eclipse.project.name}")
  	}
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '3.1'
}
